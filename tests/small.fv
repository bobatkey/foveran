sem : Desc → Set → Set ;
sem d = elimD (\x. Set → Set)
              (\A X. A)
              (\X. X)
              (\d1 d2 F G X. F X × G X)
              (\d1 d2 F G X. F X + G X)
              d ;

map : (F : Desc) → (α β : Set) → (α → β) → sem F α → sem F β ;
map F α β f = elimD (\F. sem F α → sem F β)
                    (\A x. x)
                    (\x. f x)
                    (\d1 d2 g h p. «g (fst p), h (snd p)»)
	            (\d1 d2 g h s. case s for x. sem (d1 “+” d2) β with
                                      { inl x. inl (g x)
	   			      ; inr x. inr (h x)
				      })
                    F ;
